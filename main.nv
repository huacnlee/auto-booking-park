use std.net.http;
use std.io;
use std.value;
use std.json;
use std.env;
use std.time;
use std.process;

/// optype: [2- check status, 3 - submit booking, 4 - list, 5 - delete]

let OPENID = env.get("BOOK_OPENID");
let ENDPOINT = env.get("BOOK_ENDPOINT");

fn querystring(obj: value.Value): string {
    let queries = [string] {};
    let items = obj.object()!;
    for (let k, v in items) {
        queries.push(`${k}=${v}`);
    }
    return queries.join("&");
}

fn send_request(path: string, payload: value.Value): value.Value? {
    let headers = http.new_headers();
    headers.set("Content-Type", "application/x-www-form-urlencoded;charset=UTF-8");

    let req = http.new_request(
        method: "POST",
        url: `${ENDPOINT}${path}`,
        headers: headers,
        body: querystring(payload).bytes()
    );

    let res = req.send();

    if (res.status() != 200) {
        io.println(`请求失败，状态码：${res.status()}, text: ${res.text()}`);
        return nil;
    } else {
        return res.json();
    }
}

fn get_orders(): string {
    let tel = env.get("BOOK_TEL")!;
    let payload = json.parse(`{
        "optype": 4,
        "openid": "${OPENID}",
        "tel": "${tel}"
    }`);
    let data = send_request("/fanMuUser/getBingdPhone", payload);

    let msgs = [string] {};

    let msg = check_available(tel);
    msgs.push(msg);

    // TODO: Flatten optional
    let logs = data?.get("carLogs")?.array()!;
    for (let log in logs) {
        let car_code = log.get("car")?.string();
        let day = log.get("day")?.string();
        let status = log.get("status")?.string();
        msgs.push(`车牌: ${car_code}, 日期: ${day}`);
        if (msgs.len() > 5) {
            break;
        }
    }

    return msgs.join("\n");
}

fn check_available(tel: string): string {
    let day = time.now().format("%Y-%m-%d");
    let data = send_request("/fanMuUser/getBingdPhone", json.parse(`{
        "optype": 2,
        "openid": "${OPENID}",
        "tel": "${tel}",
        "day": "${day}",
        "comId": 21
    }`));

    if (data == nil) {
        return "";
    }

    let can_count = data?.get("canCarNum")?.int()!;
    return `Today available: ${can_count}`;
}

fn submit_book(day: string, card_code: string, tel: string): string {
    io.println(`Submiting book for car: ${card_code} day: ${day}`);

    let data = send_request("/fanMuUser/getBingdPhone", json.parse(`{
        "optype": 3,
        "day": "${day}",
        "openid": "${OPENID}",
        "tel": "${tel}",
        "comId": 21,
        "carCodeAll": "${card_code}"
    }`));

    let msg = "";

    io.println(`submit_book res: ${data}`);
    let op_status = data?.get("opStatus")?.int();
    let is_loged = data?.get("isLoged")?.bool();

    if (op_status == 1) {
        msg = `预约成功, 车牌: ${card_code}, 日期: ${day}\n\n${data}`;
    } else {
        if (is_loged == true) {
            msg = `车牌已经有预约了: ${card_code}, 日期: ${day}\n\n${data}`;
        } else {
            msg = `预约失败, ${card_code}, 日期: ${day}\n\n${data}`;
        }
    }

    return msg;
}

fn notify_feishu(msg: string) {
    let headers = http.new_headers();
    headers.set("Content-Type", "application/json");

    let msg = msg.replace("\n", "\\n");

    let payload = json.parse(`{
        "msg_type": "text",
        "content": {
            "text": "${msg}"
        }
    }`);
    
    let res = http.post(
        url: env.get("FEISHU_WEBHOOK_URL")!,
        headers: headers,
        body: payload.to_json().bytes()
    );

    if (res.status() != 200) {
        let data = res.json();
        io.println(`\nFeishu Notify failed, body:\n${data}`);
    }
}

fn main() {
    if (OPENID == nil) {
        io.println("请设置环境变量 BOOK_OPENID");
        process.exit(1);
    }
    if (ENDPOINT == nil) {
        io.println("请设置环境变量 BOOK_ENDPOINT");
        process.exit(1);
    }

    let tel = env.get("BOOK_TEL")!;
    let tel_b = env.get("BOOK_TEL_B")!;

    check_available(tel);

    let date = time.now() + 1.days();
    let day = date.format("%Y-%m-%d");

    let car_code = env.get("BOOK_CARCODE")!;
    if (date.weekday() == 3) {
        car_code = env.get("BOOK_CARCODE1")!;
    }
    let msg = submit_book(day, car_code, tel);
    io.println(msg);
    notify_feishu(msg);

    let car_code_b = env.get("BOOK_CARCODE_B")!;
    if (date.weekday() == 4) {
        car_code_b = env.get("BOOK_CARCODE_B1")!;
    }
    let msg = submit_book(day, car_code_b, tel_b);
    io.println(msg);
    notify_feishu(msg);

    let msg = get_orders();
    io.println(msg);
    notify_feishu(msg);
}

test "test querystring" {
    let obj = json.parse(`{
        "optype": 0,
        "openid": "12345"
    }`);
    let qs = querystring(obj);
    assert_eq qs, "openid=12345&optype=0";
}